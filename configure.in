dnl ---------------------------------------------
dnl Automake/autoconf input file for Conglomerate
dnl ---------------------------------------------

AC_PREREQ(2.52)

AC_INIT(conglomerate, 0.7.13)
AC_CONFIG_SRCDIR(src/main.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

AM_CONFIG_HEADER(config.h)

AM_SANITY_CHECK
AM_MAINTAINER_MODE
AC_CANONICAL_HOST

AC_C_CONST
AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AM_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_PROG_INTLTOOL

GETTEXT_PACKAGE=AC_PACKAGE_NAME
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Package name for gettext])

ALL_LINGUAS="az ca cs da de el en_CA en_GB es fr hr ja nl no pa pl pt ru sq sr sr@Latn sv"

AM_GLIB_GNU_GETTEXT

dnl --- Package configuration ---

LIBXML_REQUIRED=2.0.0
LIBXSLT_REQUIRED=1.0.0
LIBBONOBO_REQUIRED=2.0.0
LIBBONOBOUI_REQUIRED=2.0.0
LIBGNOMEUI_REQUIRED=2.0.0
LIBGNOMEPRINT_REQUIRED=1.116.0
LIBGNOMEPRINTUI_REQUIRED=1.116.0
LIBGLADE2_REQUIRED=2.0.0
LIBFO_REQUIRED=0.2.3
GCONF2_REQUIRED=1.2.0
GTKSOURCEVIEW_REQUIRED=0.6

dnl --- Fundamentals ---

PKG_CHECK_MODULES(CONGLOMERATE,		\
  gconf-2.0 >= $GCONF2_REQUIRED		\
  libxml-2.0 >= $LIBXML_REQUIRED	\
  libxslt >= $LIBXSLT_REQUIRED		\
  libbonobo-2.0 >= $LIBBONOBO_REQUIRED	\
  libbonoboui-2.0 >= $LIBBONOBOUI_REQUIRED	\
  libgnomeui-2.0 > $LIBGNOMEUI_REQUIRED	\
  libglade-2.0 >= $LIBGLADE2_REQUIRED)

AC_SUBST(CONGLOMERATE_LIBS)
AC_SUBST(CONGLOMERATE_CFLAGS)

dnl --- Printing support ---

AC_MSG_CHECKING([whether to build with printing support])
AC_ARG_ENABLE(printing, [  --enable-printing      build with printing support [default=no]], enable_printing="$enableval", enable_printing=no)
                                                                                               
if test x$enable_printing = xauto || test x$enable_printing = xyes ; then
   PKG_CHECK_MODULES(PRINTING, \
		    libgnomeprint-2.2 >= $LIBGNOMEPRINT_REQUIRED \
		    libgnomeprintui-2.2 >= $LIBGNOMEPRINTUI_REQUIRED, \
		    have_printing=yes, have_printing=no)
   if test x"$have_printing" = "xyes"; then
         enable_printing=yes
	 PRINTING_CFLAGS="$PRINTING_CFLAGS -DENABLE_PRINTING"
   else
         enable_printing=no 
	 PRINTING_CFLAGS=
	 PRINTING_LIBS=
   fi
else
   enable_printing=no         
   PRINTING_CFLAGS=
   PRINTING_LIBS=
fi

AM_CONDITIONAL(ENABLE_PRINTING, test x$enable_printing = xyes)
AC_SUBST(PRINTING_CFLAGS)
AC_SUBST(PRINTING_LIBS)
AC_MSG_RESULT($enable_printing)

dnl --- libfo support ---

AC_MSG_CHECKING([whether to build with libfo support])
AC_ARG_ENABLE(libfo, [  --enable-libfo      build with libfo support [default=no]], enable_libfo="$enableval", enable_libfo=no)
                                                                                               
if test x$enable_libfo = xauto || test x$enable_libfo = xyes ; then
   PKG_CHECK_MODULES(LIBFO, \
		    libfo-0.2 >= $LIBFO_REQUIRED, \
		    have_libfo=yes, have_libfo=no)
   if test x"$have_libfo" = "xyes"; then
         enable_libfo=yes
	 LIBFO_CFLAGS="$LIBFO_CFLAGS -DENABLE_LIBFO"
   else
         enable_libfo=no 
	 LIBFO_CFLAGS=
	 LIBFO_LIBS=
   fi
else
   enable_libfo=no         
   LIBFO_CFLAGS=
   LIBFO_LIBS=
fi

AM_CONDITIONAL(ENABLE_LIBFO, test x$enable_libfo = xyes)
AC_SUBST(LIBFO_CFLAGS)
AC_SUBST(LIBFO_LIBS)
AC_MSG_RESULT($enable_libfo)

dnl --- GtkSourceView support ---

AC_MSG_CHECKING([whether to build with GtkSourceView support])
AC_ARG_ENABLE(gtksourceview, [  --enable-gtksourceview      build with GtkSourceView support [default=yes]], enable_gtksourceview="$enableval", enable_gtksourceview=yes)

if test x$enable_gtksourceview = xauto || test x$enable_gtksourceview = xyes ; then
        PKG_CHECK_MODULES(GTKSOURCEVIEW, \
                          gtksourceview-1.0 >= $GTKSOURCEVIEW_REQUIRED, \
                          have_gtksourceview=yes, have_gtksourceview=no)
        if test x"$have_gtksourceview" = "xyes"; then
                enable_gtksourceview=yes
                GTKSOURCEVIEW_CFLAGS="$GTKSOURCEVIEW_CFLAGS -DENABLE_GTKSOURCEVIEW"
        else
                enable_gtksourceview=no
                GTKSOURCEVIEW_CFLAGS=
                GTKSOURCEVIEW_LIBS=
        fi
else
        enable_gtksourceview=no
        GTKSOURCEVIEW_CFLAGS=
        GTKSOURCEVIEW_LIBS=
fi

AM_CONDITIONAL(ENABLE_GTKSOURCEVIEW, test x$enable_gtksourceview = xyes)
AC_SUBST(GTKSOURCEVIEW_CFLAGS)
AC_SUBST(GTKSOURCEVIEW_LIBS)
AC_MSG_RESULT($enable_gtksourceview)

 
dnl -- gconf, kudos to metacity
 
AC_ARG_ENABLE(gconf,              [  --disable-gconf             disable gconf usage, effects unknown leave it enabled],,enable_gconf=yes)

if test x$enable_gconf = xyes; then
  AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
  if test x"$GCONFTOOL" = xno; then
    AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
  fi
  AM_GCONF_SOURCE_2
fi


dnl --- General compilation options ---

AC_MSG_CHECKING([whether to support debugging])
AC_ARG_ENABLE(debug,
[  --enable-debug          enable debugging of application],
    debug=$enableval, debug=no)
AC_MSG_RESULT($debug)

AC_MSG_CHECKING([whether to support profiling])
AC_ARG_ENABLE(profile,
[  --enable-profile        enable profiling of application],
    profile=$enableval, profile=no)
AC_MSG_RESULT($profile)

AC_MSG_CHECKING([whether to optimize])
AC_ARG_ENABLE(optimization,
[  --enable-optimization        enable optimization of application],
    optimize=$enableval, optimize=no)
AC_MSG_RESULT($optimize)

if test "$debug" = yes; then
  BASE_CFLAGS="$BASE_CFLAGS -g"
fi

if test "$profile" = yes; then
  BASE_CFLAGS="$BASE_CFLAGS -pg"
fi

if test "$optimize" = yes; then
  BASE_CFLAGS="$BASE_CFLAGS -O3 -fomit-frame-pointer"

  case "${target}" in
    i486*) BASE_CFLAGS="$BASE_CFLAGS -march=i486 -mcpu=i486"
           ;;
    i586*) BASE_CFLAGS="$BASE_CFLAGS -march=i586 -mcpu=i586"
           ;;
    i686*) BASE_CFLAGS="$BASE_CFLAGS -march=i686 -mcpu=i686"
           ;;
  esac
fi

dnl --- bugzilla 134747
AC_MSG_CHECKING([whether to compile with -Werror])
AC_ARG_ENABLE(werror, [  --enable-werror      compile with -Werror [default=no]], enable_werror="$enableval", enable_werror=no)
AM_CONDITIONAL(ENABLE_WERROR, test x$enable_werror = xyes)
AC_MSG_RESULT($enable_werror)

dnl --- Set compiler flags ---

BASE_CFLAGS="$BASE_CFLAGS -Wall"

##################################################
# Check for gtk-doc.
#  see also
#  http://bugzilla.gnome.org/show_bug.cgi?id=131600
##################################################
GTK_DOC_CHECK

AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi

AC_SUBST(HTML_DIR)

gtk_doc_min_version=1.0
AC_MSG_CHECKING([gtk-doc version >= $gtk_doc_min_version])
if pkg-config --atleast-version=$gtk_doc_min_version gtk-doc; then
  AC_MSG_RESULT(yes)
  GTKDOC=true
else
  AC_MSG_RESULT(no)
  GTKDOC=false
fi

dnl Let people enable the gtk-doc stuff.
AC_ARG_ENABLE(gtk-doc, [  --enable-gtk-doc  Use gtk-doc to build documentation [default=no]], enable_gtk_doc="$enableval", enable_gtk_doc=no)

AM_CONDITIONAL(ENABLE_GTK_DOC, test x$enable_gtk_doc = xyes)


dnl --- Output ---

AC_OUTPUT(Makefile 
	  conglomerate.spec
          po/Makefile.in
	  data/conglomerate.keys
	  data/conglomerate.schemas
	  data/Makefile
	  debian/Makefile
	  debian/patches/Makefile
	  dispspecs/Makefile
	  doc/Makefile
	  doc/devel/Makefile
	  doc/C/Makefile
	  doc/reference/Makefile
	  doc/reference/version.xml
	  glade/Makefile
	  pixmaps/Makefile
	  examples/Makefile 
	  templates/Makefile 
          src/Makefile)

if test x$enable_gconf = xno; then
        echo "*** WARNING WARNING WARNING WARNING WARNING"
        echo "*** Building without GConf"
        echo "*** This means there's no way to change prefs except"
        echo "*** hacking source code, at least for now."
        echo "*** Also, some prefs may have broken defaults."
fi

dnl ==========================================================================
echo "
conglomerate-$VERSION:
 
                          prefix: ${prefix}
            source code location: ${srcdir}
                        compiler: ${CC}
 
                           GConf: ${enable_gconf}
                  gtk_doc enable: ${enable_gtk_doc}
                 printing enable: ${enable_printing}
                    libfo enable: ${enable_libfo}
            gtksourceview enable: ${enable_gtksourceview}
"

dnl -- end of configure.in
