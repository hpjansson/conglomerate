dnl ---------------------------------------------
dnl Automake/autoconf input file for Conglomerate
dnl ---------------------------------------------

AC_PREREQ(2.52)

AC_INIT(conglomerate, 0.3.110)
AC_CONFIG_SRCDIR(src/main.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

AM_CONFIG_HEADER(config.h)

AM_SANITY_CHECK
AM_MAINTAINER_MODE
AC_CANONICAL_HOST

AC_C_CONST
AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AM_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_PROG_INTLTOOL

GETTEXT_PACKAGE=$PACKAGE_NAME
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Package name for gettext])

ALL_LINGUAS="ru"

AM_GLIB_GNU_GETTEXT

dnl --- Package configuration ---

LIBXML_REQUIRED=2.0.0
LIBXSLT_REQUIRED=1.0.0
GNOMEUI_REQUIRED=2.0.0
BONOBOUI_REQUIRED=1.0.0
EEL_REQUIRED=2.0.0

PKG_CHECK_MODULES(CONGLOMERATE,		\
  libxml-2.0 >= $LIBXML_REQUIRED	\
  libxslt >= $LIBXSLT_REQUIRED		\
  libbonoboui-2.0 > $BONOBOUI_REQUIRED	\
  libgnomeui-2.0 > $GNOMEUI_REQUIRED	\
  eel-2.0 >= $EEL_REQUIRED)

AC_SUBST(CONGLOMERATE_LIBS)
AC_SUBST(CONGLOMERATE_CFLAGS)


dnl --- General compilation options ---

AC_MSG_CHECKING([whether to support debugging])
AC_ARG_ENABLE(debug,
[  --enable-debug          enable debugging of application],
    debug=$enableval, debug=no)
AC_MSG_RESULT($debug)

AC_MSG_CHECKING([whether to support profiling])
AC_ARG_ENABLE(profile,
[  --enable-profile        enable profiling of application],
    profile=$enableval, profile=no)
AC_MSG_RESULT($profile)

AC_MSG_CHECKING([whether to optimize])
AC_ARG_ENABLE(optimization,
[  --enable-optimization        enable optimization of application],
    optimize=$enableval, optimize=no)
AC_MSG_RESULT($optimize)

if test "$debug" = yes; then
  BASE_CFLAGS="$BASE_CFLAGS -g"
fi

if test "$profile" = yes; then
  BASE_CFLAGS="$BASE_CFLAGS -pg"
fi

if test "$optimize" = yes; then
  BASE_CFLAGS="$BASE_CFLAGS -O3 -fomit-frame-pointer"

  case "${target}" in
    i486*) BASE_CFLAGS="$BASE_CFLAGS -march=i486 -mcpu=i486"
           ;;
    i586*) BASE_CFLAGS="$BASE_CFLAGS -march=i586 -mcpu=i586"
           ;;
    i686*) BASE_CFLAGS="$BASE_CFLAGS -march=i686 -mcpu=i686"
           ;;
  esac
fi

dnl --- Set compiler flags ---

BASE_CFLAGS="$BASE_CFLAGS -Wall"

dnl --- Output ---

AC_OUTPUT(Makefile 
          po/Makefile.in
	  data/Makefile 
          src/Makefile)
